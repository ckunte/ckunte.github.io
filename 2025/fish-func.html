<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>Chetan Kunté: Fish functions</title>
<meta name="description" content="For system information: sbcinf.fish function sbcinf set..." />
<script src="/inc/code-math.js" integrity="sha384-xT8wN8NCeuamUQtBjQWvwc7dCGAT89VKkZR5MVAcr18p99euBN7VZVb0NH07+lYA"></script>
<link rel="icon" href="data:," />
<link type="text/css" rel="stylesheet" href="/inc/home.css" />
</head>
<body>
<main>
<header>
    <a accesskey="/" title="Home" href="/"><img alt="Chetan Kunté" class="logo" src="/inc/favicon.svg" width="26" height="21.37"></a>
</header>	
<article>
<h1>Fish functions</h1>
<time datetime="2025-10-30T22:43:00+08:00">
30 Oct 2025, 
3 min</time>
<ol>
<li>
<p>For system information: <em>sbcinf.fish</em></p>
<pre><code>function sbcinf
    set -l mem (free -h | awk '/Mem:/ {print $3 "/" $2}')
    set -l dsk (df -h | awk '/\/$/ {print $3 "/" $2}')
    set -l tem (vcgencmd measure_temp | grep -o -E '[[:digit:]].*')
    set -l pow (vcgencmd measure_volts | sed 's/volt=//g')
    set -l ipa (hostname -I | awk '{print $1}')
    set -l upt (uptime -p | sed 's/^up //')

    echo "Board temp: $tem"
    echo "Disk usage: $dsk"
    echo "IP address: $ipa"
    echo "Memory use: $mem"
    echo "Power cons: $pow"
    echo "Sys uptime: $upt"
end
</code></pre>
</li>
<li>
<p>For downloading a file from GitHub repo. with <code>ghdl user/repo/file.ext</code>. If specific about a branch, then this works too: <code>ghdl user/repo@master/file.ext</code></p>
<pre><code>function ghdl
    if test (count $argv) -ne 1
        echo "Usage: ghdl user/repo[@branch]/path/to/file"
        return 1
    end

    set input $argv[1]

    # Split the input into repo and path
    set repo_part (echo $input | cut -d'/' -f1-2)
    set path_part (echo $input | cut -d'/' -f3-)

    # Extract user and repo (and optional branch)
    set user (echo $repo_part | cut -d'/' -f1)
    set repo_branch (echo $repo_part | cut -d'/' -f2)

    if string match -q '*@*' $repo_branch
        set repo (echo $repo_branch | cut -d'@' -f1)
        set branch (echo $repo_branch | cut -d'@' -f2)
    else
        set repo $repo_branch
        set branch "master"  # default fallback branch
    end

    if test -z "$user" -o -z "$repo" -o -z "$path_part"
        echo "Error: Must be in the form user/repo[@branch]/path/to/file"
        return 1
    end

    # Construct modern raw.githubusercontent.com URL
    set url "https://raw.githubusercontent.com/$user/$repo/refs/heads/$branch/$path_part"

    echo "Downloading from: $url"
    curl -LO $url
end
</code></pre>
</li>
<li>
<p>Push functions</p>
<pre><code># push function
#   gp master // normal push to master repo
#   gpp bb    // rebased push to bb repo
#
function gp
  git push -u origin $argv[1]
end
function gpp
  git push -u origin +$argv[1]
end
</code></pre>
</li>
<li>
<p>Rebase function</p>
<pre><code># rebase (master or bb branches)
# examples:
#
#   gr 4 master
#   gr 3 bb
#
function gr
  git rebase -i origin/$argv[2]~$argv[1] $argv[2]
end
</code></pre>
</li>
<li>
<p>Get the latest release version of a GitHub repo.</p>
<pre><code># e.g. glr "ckunte/tce"
function glr \
    --argument-names user_repo
    curl \
        --silent \
        "https://api.github.com/repos/$user_repo/releases/latest" \
    | string match --regex '"tag_name": "\K.*?(?=")'
end
</code></pre>
</li>
<li>
<p>Check file hash</p>
<pre><code># Check hashes
function check_hashes
    # Ensure the user provided a file argument
    if test (count $argv) -ne 1
        echo "Usage: check_hashes &lt;file&gt;"
        return 1
    end
    # Store the file path
    set file $argv[1]
    # Check if the file exists
    if not test -e $file
        echo "Error: File '$file' does not exist."
        return 1
    end
    # Calculate and display the RMD160 hash
    echo "RMD160 hash:"
    printf "%s" "$file" | openssl dgst -rmd160
    # Calculate and display the SHA256 hash
    echo "SHA256 hash:"
    printf "%s" "$file" | openssl dgst -sha256
end
</code></pre>
</li>
</ol>
</article>
<footer>
  <span>&lsaquo; <a accesskey="j" title="shell functions for rebase" href="/2025/rebase-func">Prev</a></span>
  <span><a accesskey="k" href="/">Home</a> &rsaquo;</span>
</footer>
</main>
</body>
</html>